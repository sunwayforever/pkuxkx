#class dazuo open
#tab dz.start;
#tab dz.stop;

#var dz_total_count 0;
#var dz_max 95;
#alias {dz.cancel} {
    #class dz.full.inner kill;
    #unticker {dz};
};

#alias {calc_dazuo_time} {
    #format {dz_current_time} {%T};
    #if {${dz_last_time} == 0} {
        #var dz_last_time ${dz_current_time};
        #return;
    };
    #math delta ${dz_current_time} - ${dz_last_time};
    #var dz_last_time ${dz_current_time};

    #math dz_remain_nl ${max_neili} - ${hpbrief_max_nl};
    #math dz_remain_time (${dz_remain_nl} * ${delta})/60;
    #if {${dz_remain_time} < 60} {
        #var dz_remain_time ${dz_remain_time} m;
    };
    #else {
        #math dz_remain_time ${dz_remain_time}/60;
        #var dz_remain_time ${dz_remain_time} h;
    };
};

#alias {dz.rest} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #action {你的内力增加了} {
        dz.rest;
    };
    #ticker {dz} {
        yun recover; dazuo ${dazuo_count};
    } {1};
    #class dz.full.inner close;
    #math max_neili ${youxiao_neigong} * 10 - 10;
    #if {${max_neili} < ${hpbrief_max_nl}} {
        #class dz.full.inner kill;
        tmp_dz;
    };
};

#alias {dz.idle} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #action {你的内力增加了} {
        #class dz.full.inner kill;
        #delay {dz_delay} {
            tmp_dz;
        } {3};
    };
    #ticker {dz} {
        yun recover; dazuo ${dazuo_count};
    } {1};
    #class dz.full.inner close;
    #math max_neili ${youxiao_neigong} * 10 - 10;
    #if {${max_neili} < ${hpbrief_max_nl}} {
        #class dz.full.inner kill;
        #delay {dz_delay} {
            tmp_dz;
        } {@random{40}};
    };
};

#alias {dz.full} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #class dz.full.inner close;
    #delay {dz_delay} {
        #ticker {dz} {
            #if {${nl_percent} < ${dz_max}} {
                yun recover; dazuo ${dazuo_count};
            };
            #else {
                #unticker {dz};
                #class dz.full.inner kill;
                #delay {dz_delay} {
                    tmp_dz;
                    #showme dz.full;
                } {3};
            };
        } {1};
    }{1};
};

#alias {dz.start} {
    set_last_quest dz;
    autoheal.off;
    #var dz_last_time 0;
    #var dz_remain_time nil;
    #class dz.inner open;
    #alias {start_ticker} {
        #ticker {dz} {
            dazuo ${dazuo_count};
        } {1};
    };

    #alias {stop_ticker} {
        #unticker {dz};
    };

    #action {你现在的气太少了} {
        stop_ticker;
        sleep;
        on_wakeup {
            start_ticker;
        };
    };

    #action {你的内力增加了} {
        #math dz_total_count ${dz_total_count}+1;
        show_to_info force +${dz_total_count};
        #send {jifa};
        #delay {2} {
            dz.check;
        };
    };
    #class dz.inner close;
    #send {jifa};
    gt bs;
    on_there {
        chifan;
        on_health {
            dz.check;
        };
    };
};

#alias {dz.check} {
    stop_ticker;
    chifan;
    burden.check {
        dz.check.inner;
    };
};

#alias {dz.check.inner} {
    #math max_neili ${youxiao_neigong} * 10 -1;
    calc_dazuo_time;
    #if {${max_neili} < ${hpbrief_max_nl}} {
        dz.stop;
        #showme MISSION_COMPLETE: dz;
        show_to_quest 内力到达瓶颈: ${hpbrief_max_nl} => ${max_neili};
    };
    #else {
        #if {@getburden{酥油茶} < 30} {
            dz.stop;
            chain get.qian get.fan dz.start;
            #return;
        };
        start_ticker;
        show_to_quest 打坐: ${hpbrief_max_nl} => ${max_neili} 还需: ${dz_remain_time};
    };
};

#alias {dz.stop} {
    unset_last_quest;
    autoheal.on;
    stop_ticker;
    show_to_quest 打坐完毕;
    #class dz.inner kill;
};

#var dz_dwim 100;
#alias {dz.dwim} {
    #if {${nl_percent} < 50} {
        #math dz_dwim ${dz_dwim} + ${hpbrief_max_nl}/200;
    };
    #else {
        #math dz_dwim ${dz_dwim} - ${hpbrief_max_nl}/500;
    };
    #if {${dz_dwim} < 10} {
        #var dz_dwim 100;
    };
    dazuo ${dz_dwim};
};
#class dazuo close